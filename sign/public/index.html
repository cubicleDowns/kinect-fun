<html>
<head>
    <title>Twilio</title>
    <script id="shader-vs" type="x-shader/x-vertex">
  attribute vec3 aPos;
  attribute vec2 aTexCoord;
  varying   vec2 uv;
varying vec2 uv_orig;
void main(void) {
   gl_Position = vec4(aPos, 1.);
   uv = aTexCoord;
   uv_orig = uv;
}



    </script>

    <script id="shader-fs-inc" type="x-shader/x-fragment">
#ifdef GL_ES
precision mediump float;
#endif

varying vec2 uv;
varying vec2 uv_orig;
uniform vec2 aspect;
uniform vec2 pixelSize;

bool is_onscreen(vec2 uv){
	return (uv.x < 1.) && (uv.x > 0.) && (uv.y < 1.) && (uv.y > 0.);
}

float border(vec2 uv, float border, vec2 texSize){
	uv*=texSize;
	return (uv.x<border || uv.x>texSize.x-border || uv.y<border || uv.y >texSize.y-border) ? 1.:.0;
}

#define pi 3.141592653589793238462643383279
#define pi_inv 0.318309886183790671537767526745
#define pi2_inv 0.159154943091895335768883763372

float border(vec2 domain, float thickness){
   vec2 uv = fract(domain-vec2(0.5));
   uv = min(uv,1.-uv)*2.;
   return clamp(max(uv.x,uv.y)-1.+thickness,0.,1.)/(thickness);
}

float square_mask(vec2 domain){
	return (domain.x <= 1. && domain.x >= 0. && domain.y <= 1. && domain.y >= 0.) ? 1. : 0.;
}

vec2 complex_mul(vec2 factorA, vec2 factorB){
   return vec2( factorA.x*factorB.x - factorA.y*factorB.y, factorA.x*factorB.y + factorA.y*factorB.x);
}

vec2 spiralzoom(vec2 domain, vec2 center, float n, float spiral_factor, float zoom_factor, vec2 pos){
	vec2 uv = domain - center;
	float d = length(uv);
	return vec2( atan(uv.y, uv.x)*n*pi2_inv + d*spiral_factor, -log(d)*zoom_factor) + pos;
}

vec2 complex_div(vec2 numerator, vec2 denominator){
   return vec2( numerator.x*denominator.x + numerator.y*denominator.y,
                numerator.y*denominator.x - numerator.x*denominator.y)/
          vec2(denominator.x*denominator.x + denominator.y*denominator.y);
}

// HSL to RGB converter code from http://www.gamedev.net/topic/465948-hsl-shader-glsl-code/
float Hue_2_RGB(float v1, float v2, float vH )
{
	float ret;
   if ( vH < 0.0 )
     vH += 1.0;
   if ( vH > 1.0 )
     vH -= 1.0;
   if ( ( 6.0 * vH ) < 1.0 )
     ret = ( v1 + ( v2 - v1 ) * 6.0 * vH );
   else if ( ( 2.0 * vH ) < 1.0 )
     ret = ( v2 );
   else if ( ( 3.0 * vH ) < 2.0 )
     ret = ( v1 + ( v2 - v1 ) * ( ( 2.0 / 3.0 ) - vH ) * 6.0 );
   else
     ret = v1;
   return ret;
}

vec3 hsl2rgb(float H, float S, float L){
	float var_2, var_1, R, G, B;
	if (S == 0.0)
	{
	   R = L;
	   G = L;
	   B = L;
	}
	else
	{
	   if ( L < 0.5 )
	   {
		   var_2 = L * ( 1.0 + S );
	   }
	   else
	   {
		   var_2 = ( L + S ) - ( S * L );
	   }

	   var_1 = 2.0 * L - var_2;

	   R = Hue_2_RGB( var_1, var_2, H + ( 1.0 / 3.0 ) );
	   G = Hue_2_RGB( var_1, var_2, H );
	   B = Hue_2_RGB( var_1, var_2, H - ( 1.0 / 3.0 ) );
	}
	return vec3(R,G,B);
}

vec2 uv_zoom(vec2 uv, vec2 center, float zoom, float zoom_exp, float zoom_factor){
  return center + (uv - center)*(1. - zoom * pow(zoom_exp, zoom_factor*length(uv-center)));
}

vec2 gradient(sampler2D sampler, vec2 uv, vec2 d, vec4 selector){
	vec4 dX = texture2D(sampler, uv + vec2(1.,0.)*d) - texture2D(sampler, uv - vec2(1.,0.)*d);
	vec4 dY = texture2D(sampler, uv + vec2(0.,1.)*d) - texture2D(sampler, uv - vec2(0.,1.)*d);
	return -vec2( dot(dX, selector), dot(dY, selector) );
}

vec2 rot90(vec2 vector){
	return vector.yx*vec2(1,-1);
}

float atan2(float y, float x){
	if(x>0.) return atan(y/x);
	if(y>=0. && x<0.) return atan(y/x) + pi;
	if(y<0. && x<0.) return atan(y/x) - pi;
	if(y>0. && x==0.) return pi/2.;
	if(y<0. && x==0.) return -pi/2.;
	if(y==0. && x==0.) return pi/2.; // undefined usually
	return pi/2.;
}

vec2 uv_polar(vec2 uv, vec2 center){
	vec2 c = uv - center;
	float rad = length(c);
	float ang = atan2(c.x,c.y);
	return vec2(ang, rad);
}

vec2 uv_lens_half_sphere(vec2 uv, vec2 position, float radius, float refractivity){
	vec2 polar = uv_polar(uv, position);
	float cone = clamp(1.-polar.y/radius, 0., 1.);
	float halfsphere = sqrt(1.-pow(cone-1.,2.));
	float w = atan2(1.-cone, halfsphere);
	float refrac_w = w-asin(sin(w)/refractivity);
	float refrac_d = 1.-cone - sin(refrac_w)*halfsphere/cos(refrac_w);
	vec2 refrac_uv = position + vec2(sin(polar.x),cos(polar.x))*refrac_d*radius;
	return mix(uv, refrac_uv, float(length(uv-position)<radius));
}

vec2 torus_mirror(vec2 uv){
	return vec2(1.)-abs(fract(uv*.5)*2.-1.);
}

float circle(vec2 uv, vec2 aspect, float scale){
	return clamp( 1. - length((uv-0.5)*aspect*scale), 0., 1.);
}

float sigmoid(float x) {
	return 2./(1. + exp2(-x)) - 1.;
}

float smoothcircle(vec2 uv, vec2 aspect, float radius, float ramp){
	return 0.5 - sigmoid( ( length( (uv - 0.5) * aspect) - radius) * ramp) * 0.5;
}

vec4 sat(sampler2D sampler_sat, vec2 pos, vec2 size){
	vec4 s = vec4(0);
	s += texture2D(sampler_sat, pos );
	s += texture2D(sampler_sat, pos + size );
	s -= texture2D(sampler_sat, pos + size * vec2(1, 0) );
	s -= texture2D(sampler_sat, pos + size * vec2(0, 1) );
	return s / ( size.x * size.y );
}

vec4 boxblur(sampler2D sampler_sat, vec2 uv, vec2 pixelSize, float boxsize){
	return sat(sampler_sat, uv - boxsize*0.5*pixelSize, boxsize*pixelSize) * pixelSize.x * pixelSize.y;
}

float line_segment(vec2 domain, vec2 p1, float d1, vec2 p2, float d2){
  float h = 1./(p2.x-p1.x); // helper registers
  float h1 = (p2.y-p1.y)*h;
  float h2 = 1./h1;
  float xs = (-p1.y+h1*p1.x+h2*domain.x+domain.y)/(h2+h1);// coordinates of the point on the line between p1 and p2,
  float ys = -h2*(xs-domain.x)+domain.y;					// ^ orthogonally to the given point in the domain
  float d = length(domain-vec2(xs,ys));		// the orthogonal distance from the domain point to the line (unlimited)
  float s = 0.; // distance from domain point to p1 relative to p2
  if(p2.x == p1.x){	// division by zero fix
    d = abs(domain.x - p1.x);
    s = (p1.y-ys)/(p1.y-p2.y);
  }else{
    s = (xs-p1.x)*h;
  }
  d = clamp(d*(d1*(1.-s)+d2*s),0., 1.);	// adjusting the line thickness using a linear interpolation with s
  float m1 = 0.; if(s > 0.) m1 = 1.; 		// masking out the segment between p1 and p2
  float m2 = 0.; if(s < 1.) m2 = 1.;
  float result = clamp( m1*m2-d, 0., 1.); // return result as 1-distance in the range [0..1]
  result = clamp(1.-length(domain-vec2(p1.x,p1.y))*d1-m1, result, 1.);	// round corners if you will (half circles)
  //result = clamp(1.-length(domain-vec2(p2.x,p2.y))*d2-m2, result, 1.);

  return result;
}

float conetip(vec2 uv, vec2 pos, float size, float min)
{
	return max( min, 1. - length((uv - pos) * aspect / size) );
}

float warpFilter(vec2 uv, vec2 pos, float size, float ramp)
{
	return 0.5 + sigmoid( conetip(uv, pos, size, -16.) * ramp) * 0.5;
}

vec2 vortex_warp(vec2 uv, vec2 pos, float size, float ramp, vec2 rot)
{
	vec2 pos_correct = 0.5 + (pos - 0.5);
	vec2 rot_uv = pos_correct + complex_mul((uv - pos_correct)*aspect, rot)/aspect;
	float filter = warpFilter(uv, pos_correct, size, ramp);
	return mix(uv, rot_uv, filter);
}

vec2 vortex_pair_warp(vec2 uv, vec2 pos, vec2 vel)
{
	float ramp = 4.;

	float d = 0.138 / 1024. / pixelSize.y;

	float l = length(vel);
	vec2 p1 = pos;
	vec2 p2 = pos;

	if(l > 0.){
		vec2 normal = normalize(vel.yx * vec2(-1., 1.))/aspect;
		p1 = pos - normal * d / 2.;
		p2 = pos + normal * d / 2.;
	}

	float w = l / d * 2.;

	// two overlapping rotations that would annihilate when they were not displaced.
	vec2 circle1 = vortex_warp(uv, p1, d, ramp, vec2(cos(w),sin(w)));
	vec2 circle2 = vortex_warp(uv, p2, d, ramp, vec2(cos(-w),sin(-w)));
	return (circle1 + circle2) / 2.;
}

//
// Description : Array and textureless GLSL 3D simplex noise function.
//      Author : Ian McEwan, Ashima Arts.
//  Maintainer : ijm
//     Lastmod : 20110409 (stegu)
//     License : Copyright (C) 2011 Ashima Arts. All rights reserved.
//               Distributed under the MIT License. See LICENSE file.
//

//uniform float time;

varying vec3 vTexCoord3D;
varying vec3 vNormal;
varying vec3 vViewPosition;

vec4 permute( vec4 x ) {
  return mod( ( ( x * 34.0 ) + 1.0 ) * x, 289.0 );
}

vec4 taylorInvSqrt( vec4 r ) {
  return 1.79284291400159 - 0.85373472095314 * r;
}

float snoise( vec3 v ) {
  const vec2 C = vec2( 1.0 / 6.0, 1.0 / 3.0 );
  const vec4 D = vec4( 0.0, 0.5, 1.0, 2.0 );

  // First corner

  vec3 i  = floor( v + dot( v, C.yyy ) );
  vec3 x0 = v - i + dot( i, C.xxx );

  // Other corners

  vec3 g = step( x0.yzx, x0.xyz );
  vec3 l = 1.0 - g;
  vec3 i1 = min( g.xyz, l.zxy );
  vec3 i2 = max( g.xyz, l.zxy );

  //  x0 = x0 - 0. + 0.0 * C
  vec3 x1 = x0 - i1 + 1.0 * C.xxx;
  vec3 x2 = x0 - i2 + 2.0 * C.xxx;
  vec3 x3 = x0 - 1. + 3.0 * C.xxx;

  // Permutations

  i = mod( i, 289.0 );
  vec4 p = permute( permute( permute(
    i.z + vec4( 0.0, i1.z, i2.z, 1.0 ) )
    + i.y + vec4( 0.0, i1.y, i2.y, 1.0 ) )
    + i.x + vec4( 0.0, i1.x, i2.x, 1.0 ) );

  // Gradients
  // ( N*N points uniformly over a square, mapped onto an octahedron.)

  float n_ = 1.0 / 7.0; // N=7

  vec3 ns = n_ * D.wyz - D.xzx;

  vec4 j = p - 49.0 * floor( p * ns.z *ns.z );  //  mod(p,N*N)

  vec4 x_ = floor( j * ns.z );
  vec4 y_ = floor( j - 7.0 * x_ );    // mod(j,N)

  vec4 x = x_ *ns.x + ns.yyyy;
  vec4 y = y_ *ns.x + ns.yyyy;
  vec4 h = 1.0 - abs( x ) - abs( y );

  vec4 b0 = vec4( x.xy, y.xy );
  vec4 b1 = vec4( x.zw, y.zw );

  vec4 s0 = floor( b0 ) * 2.0 + 1.0;
  vec4 s1 = floor( b1 ) * 2.0 + 1.0;
  vec4 sh = -step( h, vec4( 0.0 ) );

  vec4 a0 = b0.xzyw + s0.xzyw * sh.xxyy;
  vec4 a1 = b1.xzyw + s1.xzyw * sh.zzww;

  vec3 p0 = vec3( a0.xy, h.x );
  vec3 p1 = vec3( a0.zw, h.y );
  vec3 p2 = vec3( a1.xy, h.z );
  vec3 p3 = vec3( a1.zw, h.w );

  // Normalise gradients

  vec4 norm = taylorInvSqrt( vec4( dot(p0,p0), dot(p1,p1), dot(p2, p2), dot(p3,p3) ) );
  p0 *= norm.x;
  p1 *= norm.y;
  p2 *= norm.z;
  p3 *= norm.w;

  // Mix final noise value

  vec4 m = max(0.6 - vec4(dot(x0,x0), dot(x1,x1), dot(x2,x2), dot(x3,x3) ), 0.0 );
  m = m * m;
  return 42.0 * dot( m*m, vec4( dot(p0,x0), dot(p1,x1),
	dot(p2,x2), dot(p3,x3) ) );
}

float heightMap( vec3 coord ) {
  float n = abs( snoise( coord ) );

  n += 0.25   * abs( snoise( coord * 2.0 ) );
  n += 0.25   * abs( snoise( coord * 4.0 ) );
  n += 0.125  * abs( snoise( coord * 8.0 ) );
  n += 0.0625 * abs( snoise( coord * 16.0 ) );

  return n;
}



    </script>

    <script id="shader-fs-copy" type="x-shader/x-fragment">
uniform sampler2D source;
void main(void) {
	gl_FragColor = texture2D(source, uv);
}



    </script>

    <script id="shader-fs-init" type="x-shader/x-fragment">
void main(void){
	gl_FragColor = vec4(0.);
}



    </script>

    <script id="shader-fs-move-particles" type="x-shader/x-fragment">
	uniform sampler2D sampler_prev;
	uniform sampler2D sampler_prev_n;
	uniform sampler2D sampler_blur;
	uniform sampler2D sampler_blur2;
	uniform sampler2D sampler_blur3;
	uniform sampler2D sampler_blur4;
	uniform sampler2D sampler_blur5;
	uniform sampler2D sampler_blur6;
	uniform sampler2D sampler_noise;
	uniform sampler2D sampler_noise_n;
	uniform sampler2D sampler_fluid;
	uniform sampler2D sampler_particles;

	uniform vec2 scale;
	uniform vec4 rnd;
	uniform float frame;

void main(void) {
	vec4 p = texture2D(sampler_particles, uv); // location of the particle in the previous frame
	vec2 v = texture2D(sampler_fluid, p.xy).xz; // motion vector for the actual location from the advection field texture
	vec2 noise = texture2D(sampler_noise, uv + rnd.xy).xy-0.5;
	float active = 1.;//float(frame < 10000.);
	p.xy += v*scale*(1.-active); // verlet integration
	p.xy += noise*(pixelSize/32. + pixelSize*float(frame<10000.+8. && frame > 10000.-8.)*0.);
	p.xy += gradient(sampler_blur3, p.xy, pixelSize*16., vec4(0.,0.,-128.,0.))*pixelSize;//*(active);
//	p.xy += rot90(gradient(sampler_blur2, p.xy, pixelSize*8., vec4(16,0,0,0)))*pixelSize;
	// reset to random position when off canvas
	gl_FragColor = mix( p, vec4(fract(p.xyz), 1.) , vec4((p.x < 0. || p.x > 1.)||(p.y < 0. || p.y > 1.)||(p.z < 0. || p.z > 1.)||(p.w < 0. || p.w > 1.)));
}



    </script>

    <script id="shader-fs-composite" type="x-shader/x-fragment">
	uniform sampler2D sampler_prev;
	uniform sampler2D sampler_prev_n;
	uniform sampler2D sampler_blur;
	uniform sampler2D sampler_blur2;
	uniform sampler2D sampler_blur3;
	uniform sampler2D sampler_blur4;
	uniform sampler2D sampler_blur5;
	uniform sampler2D sampler_blur6;
	uniform sampler2D sampler_noise;
	uniform sampler2D sampler_noise_n;
	uniform sampler2D sampler_fluid;
	uniform sampler2D sampler_fluid_p;
	uniform sampler2D sampler_particles;
	uniform sampler2D sampler_particle_projection;
	uniform sampler2D sampler_sat;

	uniform vec4 rnd;
	uniform vec4 rainbow;
	uniform vec2 mouse;
	uniform vec2 mouseV;
	uniform float fps;
	uniform float time;
	uniform float frame;

void main(void) {

  vec2 d = pixelSize*4.;
  vec2 gx;
  gx.x = texture2D(sampler_prev, uv-vec2(1.,0.)*d).x - texture2D(sampler_prev, uv+vec2(1.,0.)*d).x;
  gx.y = texture2D(sampler_prev, uv-vec2(0.,1.)*d).x - texture2D(sampler_prev, uv+vec2(0.,1.)*d).x;

  float r_lo = texture2D(sampler_blur4, uv + gx*pixelSize *192.).r;

  //gl_FragColor = vec4(texture2D(sampler_blur, uv - gx*pixelSize*8.).x)*vec4(1.5,0.75,0.,1.);

	float r_hi = texture2D(sampler_blur, uv - gx*pixelSize *2.).r*4. - texture2D(sampler_blur6, uv + gx*pixelSize *192.*0.).r*14.;

	gl_FragColor = vec4(1.2,0.9,0.3,1.) * r_lo;
	gl_FragColor = mix(gl_FragColor, vec4(0.9,1.,1.4,1.), r_hi);

	//gl_FragColor = texture2D(sampler_prev, uv); // bypass

  gl_FragColor.a = 1.;
}



    </script>
    <script id="shader-fs-advance" type="x-shader/x-fragment">
	uniform sampler2D sampler_prev;
	uniform sampler2D sampler_prev_n;
	uniform sampler2D sampler_blur;
	uniform sampler2D sampler_blur2;
	uniform sampler2D sampler_blur3;
	uniform sampler2D sampler_blur4;
	uniform sampler2D sampler_blur5;
	uniform sampler2D sampler_blur6;
	uniform sampler2D sampler_noise;
	uniform sampler2D sampler_noise_n;
	uniform sampler2D sampler_fluid;
	uniform sampler2D sampler_particles;
	uniform sampler2D sampler_particle_projection;

	uniform vec4 rnd;
	uniform vec4 rainbow;
	uniform vec2 texSize;
	uniform vec2 mouse;
	uniform vec2 mouseV;
	uniform float fps;
	uniform float time;
	uniform float frame;

void main(void) {
  vec4 noise = texture2D(sampler_noise, uv + rnd.xy); // the noise texture itself is static. adding randomizing

	// ye olde smear finger
	// vec2 uv = mix(uv, uv-mouseV*pixelSize, smoothcircle(uv+0.5-mouse, aspect, 0.066, 128.));

	vec2 uv = vortex_pair_warp(uv, mouse, mouseV*aspect*1.5/texSize);

  vec2 d = pixelSize*3.5; // color delta between 2n+1 pixels (from blurred image)
  vec2 gx;
  gx.x = texture2D(sampler_blur3, uv-vec2(1.,0.)*d).x - texture2D(sampler_blur3, uv+vec2(1.,0.)*d).x;
  gx.y = texture2D(sampler_blur3, uv-vec2(0.,1.)*d).x - texture2D(sampler_blur3, uv+vec2(0.,1.)*d).x;

  vec2 uvr = uv + gx*7.*pixelSize; // makes a plane deformation vector to gradually shrink the red color patches at its borders

  float r = texture2D(sampler_prev, uvr).r;
  r += (texture2D(sampler_blur, uvr).r - texture2D(sampler_blur4, uv).r)*22.5/256.; // "reaction-diffusion"
  r += 2.25/256.;

  gl_FragColor.r = clamp(r, 0., 1.);

  gl_FragColor.a = 1.;
}



    </script>

    <script id="shader-fs-sat" type="x-shader/x-fragment">
uniform sampler2D sampler_sat; // the texture that is also being rendered to
uniform vec2 offset; // add current value to the other pixel, variable for the two passes (rows, columns)
void main(){
	gl_FragColor = texture2D(sampler_sat, uv) + texture2D(sampler_sat, uv + offset);
}



    </script>

    <script id="shader-fs-blur-horizontal" type="x-shader/x-fragment">
		// original shader from http://www.gamerendering.com/2008/10/11/gaussian-blur-filter-shader/
		// horizontal blur fragment shader
		uniform sampler2D src_tex;

		void main(void) // fragment
		{
			float h = pixelSize.x;
			vec4 sum = vec4(0.0);
			sum += texture2D(src_tex, vec2(uv.x - 4.0*h, uv.y) ) * 0.05;
			sum += texture2D(src_tex, vec2(uv.x - 3.0*h, uv.y) ) * 0.09;
			sum += texture2D(src_tex, vec2(uv.x - 2.0*h, uv.y) ) * 0.12;
			sum += texture2D(src_tex, vec2(uv.x - 1.0*h, uv.y) ) * 0.15;
			sum += texture2D(src_tex, vec2(uv.x + 0.0*h, uv.y) ) * 0.16;
			sum += texture2D(src_tex, vec2(uv.x + 1.0*h, uv.y) ) * 0.15;
			sum += texture2D(src_tex, vec2(uv.x + 2.0*h, uv.y) ) * 0.12;
			sum += texture2D(src_tex, vec2(uv.x + 3.0*h, uv.y) ) * 0.09;
			sum += texture2D(src_tex, vec2(uv.x + 4.0*h, uv.y) ) * 0.05;
			gl_FragColor.xyz = sum.xyz/0.98; // normalize
			gl_FragColor.a = 1.;
		}



    </script>

    <script id="shader-fs-blur-vertical" type="x-shader/x-fragment">
uniform sampler2D src_tex;

void main(void) // fragment
{
	float v = pixelSize.y;
	vec4 sum = vec4(0.0);
	sum += texture2D(src_tex, vec2(uv.x, - 4.0*v + uv.y) ) * 0.05;
	sum += texture2D(src_tex, vec2(uv.x, - 3.0*v + uv.y) ) * 0.09;
	sum += texture2D(src_tex, vec2(uv.x, - 2.0*v + uv.y) ) * 0.12;
	sum += texture2D(src_tex, vec2(uv.x, - 1.0*v + uv.y) ) * 0.15;
	sum += texture2D(src_tex, vec2(uv.x, + 0.0*v + uv.y) ) * 0.16;
	sum += texture2D(src_tex, vec2(uv.x, + 1.0*v + uv.y) ) * 0.15;
	sum += texture2D(src_tex, vec2(uv.x, + 2.0*v + uv.y) ) * 0.12;
	sum += texture2D(src_tex, vec2(uv.x, + 3.0*v + uv.y) ) * 0.09;
	sum += texture2D(src_tex, vec2(uv.x, + 4.0*v + uv.y) ) * 0.05;
    gl_FragColor.xyz = sum.xyz/0.98;
	gl_FragColor.a = 1.;
}



    </script>

    <script id="shader-fs-add-mouse-motion" type="x-shader/x-fragment">
uniform sampler2D sampler_fluid;

uniform vec2 mouse; // mouse coordinate
uniform vec2 mouseV; // mouse velocity
uniform vec2 texSize;

float mouseFilter(vec2 uv){
	return clamp( 1.-length((uv-mouse)*texSize)/2., 0. , 1.);
}

void main(void){
	vec2 v = texture2D(sampler_fluid, uv).xz;

	if(length(mouseV) > 0.)
		v = mix(v, mouseV*2., mouseFilter(uv)*0.85);

	gl_FragColor.xz = v;
}




    </script>

    <script id="shader-fs-advect" type="x-shader/x-fragment">
uniform vec2 texSize;

uniform sampler2D sampler_fluid;

const float dt = .0005;

void main(void){
	vec2 v = texture2D(sampler_fluid, uv).xz;

	vec2 D = -texSize*vec2(v.x, v.y)*dt;

   vec2 Df = floor(D),   Dd = D - Df;
   vec2 uv = uv + Df*pixelSize;

	vec2 uv0, uv1, uv2, uv3;

	uv0 = uv + pixelSize*vec2(0.,0.);
	uv1 = uv + pixelSize*vec2(1.,0.);
	uv2 = uv + pixelSize*vec2(0.,1.);
	uv3 = uv + pixelSize*vec2(1.,1.);

	vec2 v0 = texture2D(sampler_fluid, uv0).xz;
	vec2 v1 = texture2D(sampler_fluid, uv1).xz;
	vec2 v2 = texture2D(sampler_fluid, uv2).xz;
	vec2 v3 = texture2D(sampler_fluid, uv3).xz;

	v = mix( mix( v0, v1, Dd.x), mix( v2, v3, Dd.x), Dd.y);

	gl_FragColor.xz = v*(1.-border(uv, 1., texSize));
}




    </script>

    <script id="shader-fs-p" type="x-shader/x-fragment">
uniform vec2 texSize;
uniform sampler2D sampler_v;
uniform sampler2D sampler_p;
const float h = 1./1024.;

void main(void){

	vec2 v = texture2D(sampler_v, uv).xz;
	float v_x = texture2D(sampler_v, uv - vec2(1.,0.)*pixelSize).r;
	float v_y = texture2D(sampler_v, uv - vec2(0.,1.)*pixelSize).b;

	float n = texture2D(sampler_p, uv- pixelSize*vec2(0.,1.)).r;
	float w = texture2D(sampler_p, uv + pixelSize*vec2(1.,0.)).r;
	float s = texture2D(sampler_p, uv + pixelSize*vec2(0.,1.)).r;
	float e = texture2D(sampler_p, uv - pixelSize*vec2(1.,0.)).r;

	float p = ( n + w + s + e - (v.x - v_x + v.y - v_y)*h ) * .25;

	gl_FragColor.r = p;
	gl_FragColor.ba = vec2(0.); // unused
}



    </script>

    <script id="shader-fs-div" type="x-shader/x-fragment">
uniform vec2 texSize;
uniform sampler2D sampler_v;
uniform sampler2D sampler_p;

void main(void){
	float p = texture2D(sampler_p, uv).r;
	vec2 v = texture2D(sampler_v, uv).xz;
	float p_x = texture2D(sampler_p, uv + vec2(1.,0.)*pixelSize).r;
	float p_y = texture2D(sampler_p, uv + vec2(0.,1.)*pixelSize).r;

	v -= (vec2(p_x, p_y)-p)*512.;

	gl_FragColor.xz = v;
}



    </script>

    <script type="x-shader/x-vertex" id="shader-particle-renderer-vs">
attribute vec2 uv; // particle position lookup vector
uniform sampler2D sampler_particles; // particle positions in a float texture
uniform vec2 mouse;

void main() {
	gl_Position = (texture2D(sampler_particles, uv) - 0.5)*2.; // pass em flat
	gl_PointSize = 1.;
}



    </script>
    <script type="x-shader/x-fragment" id="shader-particle-renderer-fs">
void main() {
	gl_FragColor = vec4(1.0, 0.5, 0.166, 0.33);
}



    </script>

	<link type="text/css" rel="stylesheet" href="sigh.css">
    <style type="text/css">
        body {
            background-color: #000000;
            color: #FFFFFF;
        }

        a {
            color: #FFFFFF;
            font-weight: bold;
        }

        #bg {
			position: absolute;
			z-index: 10;
            width: 100%;
            height: 100%;
            background: url('twilio-20.png') no-repeat center;
            background-size: 65%;
			overflow: hidden;
        }
    </style>

	<script src="js/socket.io.js"></script>
	<script type="text/javascript" src="js/jquery-3.1.1.min.js"></script>
	<script src="js/sign.js"></script>

    <link rel="stylesheet" type="text/css" href="//cloud.typography.com/6230892/752864/css/fonts.css">
    <link rel="stylesheet" type="text/css" href="https://fonts.googleapis.com/css?family=Lekton">
    <link type="text/css" rel="stylesheet" href="archetype.css">
    <script type="text/javascript"> $(document).ready(function () {
        $('body').click(function () {
			$('body').off("click");
            makeFullScreen();
            setTimeout(function () {
                load();
            }, 1500);
        });
    });</script>
</head>
<body>
<div id="bg"></div>
<div id="desc">
</div>
<canvas id="c"></canvas>
<div id="instructions">
</div>
<div id="byJosh">
	Josh Staples:  Authy Solutions Architect
</div>
</body>
</html>
